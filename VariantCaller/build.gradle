plugins {
  id 'idea'
  id 'eclipse'
  id 'java'
  id 'application'
  id 'checkstyle'
  id "com.github.spotbugs" version "3.0.0"
  id "org.unbroken-dome.test-sets" version "2.2.1"
  id "com.github.johnrengelman.shadow" version "5.2.0"
}

mainClassName = "com.epam.bioinf.variantcaller.Main"

group 'com.epam.bioinf.variantcaller'

sourceCompatibility = 1.11
targetCompatibility = 1.11

sourceSets {
  main {
    java.srcDirs = ['src/main/java']
  }
}

repositories {
  mavenCentral()
}

testSets {
  integrationTest {
    dirName = 'integrationTest'
  }
}

dependencies {
  //jopt - library for command line arguments analysis
  compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '6.0-alpha-3'

  //unit tests libs
  testCompile('junit:junit:4.12')
  testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')

  //integration tests libs
  integrationTestCompile('junit:junit:4.12')
  integrationTestImplementation('org.junit.jupiter:junit-jupiter:5.6.0')

  //spotbugs libs
  compile group: 'com.github.spotbugs', name: 'spotbugs', version: '4.0.1', ext: 'pom'

  //https://mvnrepository.com/artifact/com.github.spotbugs/spotbugs-annotations
  compileOnly 'com.github.spotbugs:spotbugs-annotations:4.0.1'

  //https://mvnrepository.com/artifact/com.github.samtools/htsjdk
  compile group: 'com.github.samtools', name: 'htsjdk', version: '2.21.3'
  implementation 'org.jetbrains:annotations:15.0'
}

integrationTest.dependsOn shadowJar

shadowJar {
  baseName = 'VariantCaller'
  classifier = ''
  archiveVersion = ''
}

jar {
  manifest.attributes("Main-Class": mainClassName)
}

test {
  useJUnitPlatform()
}

def configDir = "${project.rootDir}/config"
checkstyle {
  toolVersion '7.8.1'
  configFile file("$configDir/checkstyle/checkstyle.xml")
  configProperties.checkstyleSuppressionsPath = file("$configDir/checkstyle/suppressions.xml").absolutePath
}

spotbugs {
  toolVersion = '4.0.1'
  ignoreFailures = false
  effort = 'max'
  showProgress = false
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

checkstyleMain {
  source = 'src/main/java'
}
checkstyleTest {
  source = 'src/test/java'
}
