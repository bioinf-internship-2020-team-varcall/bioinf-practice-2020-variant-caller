plugins {
    id 'idea'
    id 'eclipse'
    id 'java'
    id 'application'
    id "org.unbroken-dome.test-sets" version "2.2.1"
    id 'checkstyle'
    id "com.github.spotbugs" version "3.0.0"
}

mainClassName = "com.epam.bioinf.variantcaller.Main"

group 'com.epam.bioinf.variantcaller'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.11
targetCompatibility = 1.11

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
    }
}

repositories {
    mavenCentral()
}
testSets {
    integrationTest {
        dirName = 'integrationTest'
    }
}

dependencies {
    //jopt - library for command line arguments analysis
    compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '6.0-alpha-3'

    //integration tests libs
    testCompile('junit:junit:4.12')
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')

    //integration tests libs
    integrationTestCompile('junit:junit:4.12')
    integrationTestImplementation('org.junit.jupiter:junit-jupiter:5.6.0')

    //spotbugs libs
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

run {
    if ( project.hasProperty("testArgs") ) {
        args Eval.me(testArgs)
    }
}

jar {
    manifest.attributes("Main-Class": mainClassName)
}

test {
    useJUnitPlatform()
}

def configDir = "${project.rootDir}/config"
checkstyle {
    toolVersion '7.8.1'
    configFile file("$configDir/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("$configDir/checkstyle/suppressions.xml").absolutePath
}

spotbugs {
    toolVersion = '4.0.0'
    ignoreFailures = false
    effort = 'max'
    showProgress = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}
